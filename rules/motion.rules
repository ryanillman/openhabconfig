var Timer bathroomNightlightTimer
var Timer kitchenNightlightTimer


rule "Kitchen light turns on when motion at night"
when
  Item KitchenMultisensor_AlarmMotion changed to ON
then
  if (State_isNight.state == "ON") {
    logInfo("kitchen.motion.nightlight", "kitchen nightlight triggered!");
    KitchenCornerLight_Dimmer.sendCommand(80);

    if (kitchenNightlightTimer !== null && !kitchenNightlightTimer.hasTerminated()) {
      kitchenNightlightTimer.cancel();
    }

    kitchenNightlightTimer = createTimer(now.plusMinutes(5), [ |
      KitchenCornerLight_Dimmer.sendCommand(0);
    ])
  }
  else {
    logInfo("kitchen.motion.nightlight", "not triggered because not night" + State_isNight.state.toString());
  }
end

rule "Upstairs Blinds Close when the sun comes around"
when
  Item LocalSun_Position_Azimuth changed   
then
  var int cloudiness =(LocalWeatherAndForecast_Current_Cloudiness.state as Number).intValue
  var int sunpos = (LocalSun_Position_Azimuth.state as Number).intValue

  if (sunpos > 122 && sunpos < 130 && cloudiness < 50) {
    logInfo("upstairs-blinds-sun-close.rule", "closing upstairs blinds to prevent the sun from coming in")
    RyansOfficeBlinds_Rollershutter.sendCommand(DOWN)
    StairwellBlinds_Rollershutter.sendCommand(DOWN)
  }

  if (sunpos > 214 && sunpos < 220) {
    RyansOfficeBlinds_Rollershutter.sendCommand(UP)
    StairwellBlinds_Rollershutter.sendCommand(UP)
  }
end


