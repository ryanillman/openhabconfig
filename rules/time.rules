var Timer  blindUpTimer
var Timer  ryanAlarmTimer
var Number ryanAlarmTarget = 20
var Number ryanAlarmStep   = 1

var Number blindsUnderManualControl = 0


rule "Ryan's Alarm On"
when
  Time cron "0 15 6 ? * MON-FRI" or
  Time cron "1 15 6 ? * MON-FRI"
then
  logInfo("ryans-alarm.rule", "In Ryan's Alarm")

  if (ryanAlarmTimer !== null && !ryanAlarmTimer.hasTerminated()) {
    logInfo("ryans-alarm.rule", "timer was already running, cancelling")
    try {
      ryanAlarmTimer.cancel();
    }
    catch (Throwable e) {
      logInfo("ryans-alarm.rule", "some sort of exception... " + e.toString());
    }
  }

  if (RyanPresent.state == ON) {
    RyanScentsyWarmer_Dimmer.sendCommand(ON);
    
    ryanAlarmTimer = createTimer(now, [ |
       if ((RyansLamp_Dimmer.state as Number) >= ryanAlarmTarget) {
         logInfo("ryans-alarm.rule", "Ryans alarm reached target, ending sequence");
         return;
       }
       else {
         RyansLamp_Dimmer.sendCommand((RyansLamp_Dimmer.state as Number) + ryanAlarmStep)
         ryanAlarmTimer.reschedule(now.plusMinutes(1))
       }
    ])
  }
  else {
     logInfo("ryans-alarm.rule", "Ryan not present, so not turning on the alarm")
  }
end


rule "Ryans Alarm Off"
when
  Time cron "0 30 7 ? * *" or
  Item RyanPresent changed to OFF
then
  RyansLamp_Dimmer.sendCommand(OFF)
end


rule "Cancel Ryans Alarm if overridden"
when
  Item RyansLamp_Dimmer received command OFF
then
  if (ryanAlarmTimer !== null) {
    ryanAlarmTimer.cancel()
  }
end


rule "Occasional Office Scentsy"
when
  Time cron "0 0 8,12,16 ? * *"
then
  if (RyanPresent.state == ON) {
    RyanScentsyWarmer_Dimmer.sendCommand(100);
    CaitScentsyWarmer.sendCommand(100);
  }
end

rule "Occasional Livingroom Scentsy"
when
  Time cron "0 0 8,10,13,16,18,20 ? * *"
then
  LivingroomWaxWarmer_Dimmer.sendCommand(100);
end


rule "Set Ryan's working status"
when 
  Time cron "0 30 6 ? * MON-FRI "
then
  logDebug("ryan_work_light", "workin' time");
  RyanWorking_Manual.postUpdate(ON);
end


rule "Clear Ryan's working status"
when 
  Time cron "0 0 18 ? * * "
then
  logDebug("ryan_work_light", "resetting flags");
  RyanInMeeting_Manual.postUpdate(OFF);
  RyanInMeeting_Cal.postUpdate(OFF);
  
  RyanWorking_Manual.postUpdate(OFF);
  RyanWorking_Cal.postUpdate(OFF);
  RyanWorking_Motion.postUpdate(OFF);
end

rule "turn off Ryan's work light"
when
  Time cron "0 0 22 ? * *"
then
  logDebug("ryan_work_light", "turning off because it's evening");
  RgbwBulb8_Color.sendCommand(0);
end


rule "Ryan Working Status Light"
when
  Item RyanWorking changed or
  Item RyanInMeeting changed
then
  logDebug("ryan_work_light", "something changed " + triggeringItemName);

  if (ON == RyanInMeeting.state) {
    logDebug("ryan_work_light", "in a meeting");
    RgbwBulb8_Color.sendCommand(new HSBType(new DecimalType(0), new PercentType(100), new PercentType(50)));
  }
  else if (ON == RyanWorking.state) {
    logDebug("ryan_work_light", "working");
    RgbwBulb8_Color.sendCommand(new HSBType(new DecimalType(50), new PercentType(100), new PercentType(50)));
  }
  else {
    logDebug("ryan_work_light", "not working");
    RgbwBulb8_ColorTemperature.sendCommand(50)
    RgbwBulb8_Color.sendCommand(50);
  }
end



rule "Porch Lights Off"
when
	Time cron "0 0 0 ? * * "
then
	FrontPorchLight1_Color.sendCommand(OFF);
	FrontPorchLight2_Color.sendCommand(OFF);
	BackPorchLight1_Color.sendCommand(OFF);
	BackPorchLight2_Color.sendCommand(OFF);
  PorchLights_Switch.sendCommand(OFF);
end


rule "Porch Lights Day"
when
	Time cron "0 0 5 ? * * "
then
	if (12 == now.getMonthValue()) {
	  FrontPorchLight1_Color.sendCommand(new HSBType(new DecimalType(120), new PercentType(100), new PercentType(20)));
    BackPorchLight1_Color.sendCommand(new HSBType(new DecimalType(120), new PercentType(100), new PercentType(20)));
    FrontPorchLight2_Color.sendCommand(new HSBType(new DecimalType(0), new PercentType(100), new PercentType(20)));
    BackPorchLight2_Color.sendCommand(new HSBType(new DecimalType(0), new PercentType(100), new PercentType(20)));
	}
	else {
		FrontPorchLight1_Color.sendCommand(20);
		FrontPorchLight2_Color.sendCommand(20);
		BackPorchLight1_Color.sendCommand(20);
		BackPorchLight2_Color.sendCommand(20);


		FrontPorchLight1_Temperature.sendCommand(20);
		FrontPorchLight2_Temperature.sendCommand(20);
		BackPorchLight1_Temperature.sendCommand(20);
		BackPorchLight2_Temperature.sendCommand(20);
	}
end

rule "Porch Lights Evening"
when
	Time cron "0 0 16 ? * * "
then
  if (12 == now.getMonthValue()) {
	  FrontPorchLight1_Color.sendCommand(new HSBType(new DecimalType(0), new PercentType(100), new PercentType(80)));
    BackPorchLight1_Color.sendCommand(new HSBType(new DecimalType(0), new PercentType(100), new PercentType(80)));
    FrontPorchLight2_Color.sendCommand(new HSBType(new DecimalType(120), new PercentType(100), new PercentType(80)));
    BackPorchLight2_Color.sendCommand(new HSBType(new DecimalType(120), new PercentType(100), new PercentType(80)));
    PorchLights_Switch.sendCommand(ON);
	}
	else {
    FrontPorchLight1_Color.sendCommand(80);
    FrontPorchLight2_Color.sendCommand(80);
    BackPorchLight1_Color.sendCommand(80);
    BackPorchLight2_Color.sendCommand(80);


    FrontPorchLight1_Temperature.sendCommand(80);
    FrontPorchLight2_Temperature.sendCommand(80);
    BackPorchLight1_Temperature.sendCommand(80);
    BackPorchLight2_Temperature.sendCommand(80);
  }
end
